'++LotusScript Development Environment:2:5:(Options):0:74
Option Declare
'******************************************************************************************************************
'* 
'*								CLASS AUDITORIA
'*
'******************************************************************************************************************
'* El formato de las bases debe ser : path/NOMBREMODULO + anio_nro.nsf   (EJ: path/Auditoria2003_2.nsf )
'* El formato de las claves debe ser : ClaveGeneral + Anio_Numero (Ej: AUDITORIA2003_05)
'*
'*	Contructor
'*
'*		Public Sub new()
'*
'* 
'*	Métodos
'*
'*		Public Sub registrar()
'*		'Genera un documento de registro de operacion en la base
'*
'*
'*		Public Property Get usuario(Byval sUser As String) As String
'*
'*
'*		Public Property Set usuario(Byval sUser As String) As String
'*
'*
'*		Public Property Get operacion(Byval sOper As String) As String
'*
'*
'*		Public Property Set operacion(Byval sOper As String) As String
'*
'*
'*		Public Property Get numeroDoc(Byval sNro As String) As String
'*
'*
'*		Public Property Set numeroDoc(Byval sNro As String) As String
'*
'*
'*
'*

Use "Configuracion Formularios"
Use "ConstantesSistema"
Use "Errores Generales"
Use "FuncionesGenerales"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class AuditoriaFormularios

'++LotusScript Development Environment:2:5:(Declarations):0:10
Const NOMBRE_MODULO				="AuditoriaFormularios"
Const FORM_OPERACION			="FDetalleRegistro"
Const NUMERO_PRIMER_BASE			=1
Const MAX_NUMERO_BASE_NOT_FOUND	=-1

Const STR_BARRA_INV				="\"

'Recordar de modificar la clase y formualrio configuracion cuando se agregguen los nuevos parametros para 
'lista de operaciones a registrar, y tiempo máximo de generación=Tiempo de vida de la base de auditoria
 
Public Class AuditoriaFormularios
	Private db As notesDatabase
	Private sUsuario As String
	Private sNroDocumento As String 
	Private sOperacion As String
	Private sParamOperacion As String
	Private sTipoDocumento As String
	Private tFecha As NotesDateTime
	Private flagCreate As Boolean
	Private NOMBRE_APLICACION As String
	
'******************************************************************************************************************	
'******************************************************************************************************************	
'Privadas
	
	Private Function tiempoCumplido(fechaCreacion As Variant) ',sClave As String) As Integer
		On Error Goto errorHandler
		Dim vClaves As New vector(0),vFechas As New Vector(0), i As Integer
		Dim dFecha As Variant
		Dim c As ConfigForms
		
		Set c = New ConfigForms
		tiempoCumplido=False
		
		Dim fNow As notesDateTime,fCreada As notesDateTime
		Set fNow=New notesDateTime(Now)
		Set fCreada= New notesDateTime(fechaCreacion)
		
		If Isnumeric(c.TiempoAuditoria) Then
			If ( Cint(fNow.Timedifference(fCreada)/86400) >= c.TiempoAuditoria ) Then
				tiempoCumplido = True
			Else
				tiempoCumplido=False
			End If	
		Else
			tiempoCumplido=False
		End If	
		
		Exit Function
errorHandler:
		'MsgBox "ClaseAuditoriaFormularios - tiempoCumplido: " & Error$ & " " & Erl
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " Clase AuditoriaFormularios - TiempoCumplido ", "")
		Exit Function
	End Function
	
'******************************************************************************************************************	
	
	Private Sub notificarBaseCreada(destino As Variant)
		On Error Goto errorHandler
		Dim v As Vector
		
		'Chequeo si me vienen destinatarios
		If Isempty(destino) Then
			Exit Sub
		End If
		Set v = New Vector(0)
		v.Values = destino
		If v.CantElem = 0 Then
			Exit Sub
		End If
		
		Dim mail As notesDocument
		Set mail=New notesDocument(db)
		mail.form="Memo"
		mail.Principal="IGDoc"
		mail.subject="Atención: Nueva base de Auditoría " 
		
		Dim rti As NotesRichTextItem
		Set rti=New NotesRichTextItem(mail,"Body")
		rti.AppendText  "Se ha creado una nueva base de auditoría. " 
		rti.AddNewline 2
		rti.AppendText "Servidor: " & db.Server
		rti.AddNewline 1
		rti.AppendText "Archivo: " & db.FilePath 
		
		
		mail.send False,destino
		
		
		Exit Sub
errorHandler:
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " Clase AuditoriaFormularios - notificarBaseCreada ", "")
		Exit Sub
	End Sub
	
'******************************************************************************************************************	
	
	Private Sub crearBase(c As ConfigForms,sNumBase As Integer) 		'Teminar busqueda de base en Conf
		On Error Goto errorHandler
		Dim template As New notesDatabase("","")
		Dim sPath As String,sFile As String,server As String
		Dim session As notesSession,ssDB As notesDatabase
		Dim searchDb As NotesDatabase
		Dim flagCreate As Integer
		
		'Obtengo el nombre y el path de la base de auditoría a crear
		Set session = New notesSession
		flagCreate = True
		sPath=""
		Set template = c.ReferenciarBaseDeDatos(CLAVE_DBAUDITORIA_TEMPLATE)
		If template.isopen Then
			If db Is Nothing Then
				Set ssDB=session.currentDatabase
				server=ssDB.server
				If Instr(ssDB.filePath,STR_BARRA_INV)>0 Then
					sPath=Strleftback(ssDB.filePath,STR_BARRA_INV)	
				End If
				sPath=Strleft(ssDB.FilePath,ssDB.FileName)
			Else				
				server=db.server
				sPath=Strleft(db.FilePath,db.FileName)
			End If
			sFile=NOMBRE_MODULO & Cstr(Year(Today)) & "_" & Cstr(sNumBase) & ".nsf"				
			
			'Chequeo si no existe, en cuyo caso la creo a partir del template
			Set db = New NotesDatabase("", "")
			If sPath<>"" Then
				If Not db.Open(server,sPath  & sFile) Then
					Set db=template.Createcopy(server,sPath  & sFile)		
				End If
			Else
				If Not db.Open(server,sFile)	Then
					Set db=template.Createcopy(server,sFile)		
				End If
			End If			
			
			If db.isOpen Then
				db.Title=NOMBRE_MODULO & " " & Cstr(Year(Today)) & " " & Cstr(sNumBase)
				'Agregar a la configuracion
				Dim vIns As New Vector(0),nColOrd As Integer,serverName As NotesName
				Dim sdoc As NotesDocument
				Dim paths As String
				
				Set serverName=New notesname(db.server)
				nColOrd=1
				vIns.insertElement CLAVE_DBAUDITORIA & Cstr(Year(Today)) & "_" & sNumBase , 1
				Dim nom As New NotesName(db.server)
				vIns.insertElement  nom.Abbreviated, 2
				vIns.insertElement c.Hosts.getElement(1) , 3  
				vIns.insertElement db.filepath , 4
				c.insertLineaInteroperabilidad vIns,nColOrd
				c.addAuditoriaDate CLAVE_DBAUDITORIA & Cstr(Year(Today))  & "_" & sNumBase,Now
				c.save
				
				Dim igdocConfigDoc As NotesDocument
				Set igdocConfigDoc = session.CurrentDatabase.GetProfileDocument("IGDocConfig")
				notificarBaseCreada igdocConfigDoc.GetItemValue("sAdministradores")
				
			
				Set sdoc= searchDb.CreateDocument
				sdoc.Form="ConfigurationForm"
				sdoc.Scope="0"
				sdoc.Domain=serverName.Canonical
				sdoc.Server=serverName.Canonical
				sdoc.Pathname=db.FilePath
				sdoc.IndexOptions="2"
				sdoc.DbViews="($All)"
				Call sdoc.ComputeWithForm(True,True)
				Call sdoc.save(True,False)
			Else
				Error ERROR_DB_NOT_CREATED,MSG_ERROR_DB_NOT_CREATED & " " & sPath & STR_BARRA_INV & sFile
			End If	
		Else
			Error ERROR_DB_NOT_CREATED,MSG_ERROR_DB_NOT_CREATED  & " " & NOMBRE_MODULO
		End If
		Exit Sub
errorHandler:
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " Clase AuditoriaFormularios - CrearBase ", "")
		Exit Sub
	End Sub	
	
'******************************************************************************************************************	
	
	Private Function maxBase(c As ConfigForms,sClave As String) As Integer
		On Error Goto errorHandler
		Dim vClaves As New Vector(0), nTmp As Integer,sNum As String
		
		Set vClaves=c.claves
		maxBase=MAX_NUMERO_BASE_NOT_FOUND
		Forall cl In vClaves.values
			If Ucase(Strleft(Cstr(cl),"_"))=CLAVE_DBAUDITORIA & Cstr(Year(Today)) Then
				sNum=Strright(Cstr(cl),"_")
				If Isnumeric(sNum) Then
					nTmp=Cint(sNum)
					If nTmp>maxBase Then
						maxBase=nTmp
					End If
				End If	
			End If
		End Forall	
		Exit Function
errorHandler:
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " Clase AuditoriaFormularios - MaxBase ", "")
		Exit Function
	End Function
	
'******************************************************************************************************************	
'******************************************************************************************************************	
'Públicas
	
	Public Sub new(aplicacion As String)
		'Se busca la base que corresponde abrir. La que tiene el máximo índice de clave
		'Si no se encuentra una, se la crea. Si se encuentra una, se verifica si no se 
		'cumplió el tiempo de vida de la misma. En caso afirmativo, se crea una.
		On Error Goto errorHandler
		
		NOMBRE_APLICACION = aplicacion
		
		Dim c As New ConfigForms, srv As String, path As String
		Dim nMax As Integer ,sClave As String
		flagCreate = False
		nMax=maxBase(c,CLAVE_DBAUDITORIA)
		
		
		If nMax=MAX_NUMERO_BASE_NOT_FOUND Then
			'No se encontró base de auditoría alguna, creo una
			crearBase c,NUMERO_PRIMER_BASE
			nMax=NUMERO_PRIMER_BASE
		Else
			sClave=CLAVE_DBAUDITORIA & Cstr(Year(Today)) & "_" & Cstr(nMax)
			'srv = c.ServidorDomino(sClave)
			'path = c.BaseDeDatos(sClave)
			Set db = c.ReferenciarBaseDeDatos(sClave)
			If Not db.isOpen Then
				'Se encontró una base en la interoperabilidad, pero no se pudo abrir, intento crearla igualmente
				'Error ERROR_DB_NOT_OPEN,MSG_ERROR_DB_NOT_OPEN & " - " & NOMBRE_APLICACION
				crearBase c, nMax
			Else
				'Se pudo abrir la base encontrada
				If tiempoCumplido(db.Created) Then 'sClave) Then 'Sacar de Configuracion c
					'Se cumplió el tiempo para la base encontrada, creo una nueva
					crearBase c, nMax+1
				End If
			End If			
		End If		
		Exit Sub
errorHandler:
		Msgbox "ERROR " & Err & " " & Error$
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " New AuditoriaFormularios", "")
		Exit Sub	
	End Sub
	
'******************************************************************************************************************	
	
	Public Sub registrar()
	'Genera un documento de registro de operacion en la base
		On Error Goto errorHandler
		
		Dim doc As notesdocument
		
		Set doc=New notesdocument(db)
		doc.form=FORM_OPERACION
		doc.nbUsuario=Me.usuario
		doc.sOperacion=Me.operacion
		doc.sNroDocumento=Me.numeroDoc
		doc.sTipoDocumento=Me.tipoDoc
		doc.sParamOperacion = Me.paramOperacion
		doc.fFecha=Now
		doc.save True,False
		If flagCreate Then
			db.UpdateFTIndex (True)
			flagCreate = False
		End If
		Exit Sub
errorhandler:
		Call RegistrarYGenerarError(NOMBRE_APLICACION, " Registrar AuditoriFormularios", "")
		Exit Sub
	End Sub
	

	
'******************************************************************************************************************		
	
	Public Property Get usuario As String
		usuario=sUsuario		
	End Property
	
'******************************************************************************************************************	
	
	Public Property Set usuario As String
		sUsuario=usuario
	End Property
	
'******************************************************************************************************************	
	
	Public Property Get operacion As String
		operacion=sOperacion	
	End Property
	
'******************************************************************************************************************	
	
	Public Property Set operacion As String
		sOperacion=operacion
	End Property
	
'******************************************************************************************************************	
	
	Public Property Get numeroDoc As String
		numeroDoc=sNroDocumento
	End Property
	
'******************************************************************************************************************	
	
	Public Property Set numeroDoc As String
		sNroDocumento=numeroDoc
	End Property
	
'******************************************************************************************************************	
	
	Public Property Get tipoDoc As String
		tipoDoc=sTipoDocumento
	End Property
	
'******************************************************************************************************************	
	
	Public Property Set tipoDoc As String
		sTipoDocumento=tipoDoc
	End Property
'******************************************************************************************************************	
	
	Public Property Get paramOperacion As String
		 paramOperacion=sParamOperacion
	End Property
	
'******************************************************************************************************************	
	
	Public Property Set paramOperacion As String
		 sParamOperacion=paramOperacion
	End Property
	
	
End Class