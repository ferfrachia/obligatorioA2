'++LotusScript Development Environment:2:5:(Options):0:74
Option Declare
Use "ConfEE 1.0"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class Informes
Declare Public Class InformesUnidad As Informes	
Declare Public Class InformesTemas As Informes

'++LotusScript Development Environment:2:5:(Declarations):0:10
Private Const VIEWINFORMEUNIDADES = 		"vInformeUnidades"
Private Const VIEWINFORMETEMAS   = 		"vInformeTemas"

Public Const INFORME_CAMPO_UNIDAD =		"sUnidad"
Public Const INFORME_CAMPO_MES=			"sMes"
Public Const INFORME_CAMPO_ANIO=			"sAnio"
Public Const INFORME_CAMPO_TEMA=			"sTema"

Public Const INFORME_CANT_EXP=			"nCantExp"
Public Const INFORME_CANT_CAR=			"nCantCar"
Public Const INFORME_TPER_EXP=			"nTPerExp"
Public Const INFORME_TTRA_EXP=			"nTTraExp"
Public Const INFORME_CANT_PASES=			"nCantPases"
Public Const INFORME_TDUR_EXP=			"nTDurExp"
Public Const INFORME_CANT_PACT=			"nCantPAct"


Public Const ERRODATABASENOTFOUND = 		19500
Public Const MSGERRORDATABASENOTFOUND = 	"No se encuentra la base de Informes"



Public Class Informes
	Private dbInforme As NotesDatabase
	Private docInf As NotesDocument	
	
	
	Public Sub SumaElementos (nombreCampo As String, valor As Double, promedio As Boolean)
		If docInf.HasItem(nombreCampo) Then
			docInf.replaceItemvalue nombrecampo, docInf.getItemValue(nombreCampo)(0)+ valor
		Else
			docInf.replaceItemvalue nombrecampo,  valor
		End If
		If promedio Then
			If docInf.HasItem(nombreCampo + "_nro") Then
				docInf.ReplaceItemValue nombreCampo + "_nro", docInf.GetItemValue(nombreCampo + "_nro")(0)+1
			Else
				docInf.ReplaceItemValue nombreCampo + "_nro", 1
			End If
		End If
		docInf.Save True,False
	End Sub
	
	Public Sub RestaElementos (nombreCampo As String, valor As Double, promedio As Boolean)
		If docInf.HasItem(nombreCampo) Then
			docInf.replaceItemvalue nombrecampo, docInf.getitemvalue(nombreCampo)(0) - valor
			If promedio Then
				docInf.ReplaceItemValue nombreCampo + "_nro", docInf.GetItemValue(nombreCampo + "_nro")(0)-1
			End If
			
			docInf.Save True,False
		End If
	End Sub
	
End Class

Public Class InformesUnidad As Informes	
	
	Public Sub new (mes As String, anio As String, unidad As String)
		Dim c As New Configuracion
		Dim ses As New NotesSession
		Dim viewInformes As NotesView
		Dim clave (1 To 3) As String
		
		Set dbInforme = New notesdatabase(ses.CurrentDatabase.Server,c.BaseDeDatos(CLAVE_DBINFORMES))
		If dbInforme Is Nothing Then
			Error ERRODATABASENOTFOUND, MSGERRORDATABASENOTFOUND
		End If
		Set viewInformes = dbInforme.getView(VIEWINFORMEUNIDADES)
		clave(1) = anio
		clave(2) = mes
		clave(3) = unidad
		
		Set docInf = viewInformes.GetDocumentByKey(clave,True)		
		If docInf Is Nothing Then
			'Crear Informe
			Set docInf = New NotesDocument (dbInforme)
			docInf.replaceItemValue "Form","fInfUnidad"
			docInf.replaceItemValue INFORME_CAMPO_UNIDAD,unidad
			docInf.replaceItemValue INFORME_CAMPO_ANIO,anio
			docInf.replaceItemValue INFORME_CAMPO_MES,mes
			docInf.save True,False
			
		End If
	End Sub
	
	
	
	
End Class

Public Class InformesTemas As Informes
	
	Public Sub new (mes As String, anio As String, tema As String)
		Dim c As New Configuracion
		Dim ses As New NotesSession
		Dim viewInformes As NotesView
		Dim clave (1 To 3) As String
		If tema="" Then
			tema = "SIN TEMA"
		End If
		
		clave(1) = anio
		clave(2) = mes
		clave(3) = tema
		
		Set dbInforme = New notesdatabase(ses.CurrentDatabase.Server,c.BaseDeDatos(CLAVE_DBINFORMES))
		If dbInforme Is Nothing Then
			Error ERRODATABASENOTFOUND, MSGERRORDATABASENOTFOUND
		End If
		Set viewInformes = dbInforme.getView(VIEWINFORMETEMAS)
		
		Set docInf = viewInformes.GetDocumentByKey(clave,True)
		
		If docInf Is Nothing Then
			'Crear Informe
			Set docInf = New NotesDocument (dbInforme)
			docInf.replaceItemValue "Form","fInfTema"
			docInf.replaceItemValue INFORME_CAMPO_TEMA,tema
			docInf.replaceItemValue INFORME_CAMPO_ANIO,anio
			docInf.replaceItemValue INFORME_CAMPO_MES,mes
			docInf.save True,False
		End If
		
	End Sub
	
End Class